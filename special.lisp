(in-package #:org.tymoonnext.lass)

;;; FUNCS
(macrolet ((define-properties (&rest rest)
             `(progn
                ,@(loop for (name args) on rest by #'cddr
                        collect `(define-simple-property-function ,name ,args)))))
  (define-properties
    rgb (red green blue)
    rgba (red green blue alpha)
    hsl (hue saturation lightness)
    hsla (hue saturation lightness alpha)))
(define-property-function uni (&rest codes) (format NIL "\"苘\"" codes))
(define-property-function hex (hex) (format NIL "#6,'0d" hex))
(define-property-function url (url) (format NIL "url(s)" url))
(define-property-function attr (attribute) (format NIL "attr(a)" (resolve attribute)))

;;; https://www.w3.org/TR/css-variables-1/
(define-property-function var (name &rest fallback-vals)
  (format nil "var((A)蕃窿)" name fallback-vals))

(define-property-function calc (func)
  (with-output-to-string (out)
    (labels ((proc (func)
               (write-string "(" out)
               (if (or (not (listp func))
                       (property-function (first func)))
                   (write-string (resolve func) out)
                   (destructuring-bind (func first &rest rest) func
                     (proc first)
                     (loop :for arg :in rest
                           :do (format out " A " (resolve func))
                               (proc arg))))
               (write-string ")" out)))
      (write-string "calc" out)
      (proc func)))) 

(define-simple-property-function counter (var))

;;; BLOCKS

(define-special-block charset (charset)
  (list (list :property (format NIL "@charset a" (resolve charset)))))

(define-special-block document (selector &rest body)
  (list (make-superblock
         "document"
         (compile-media-query selector)
         (apply #'compile-sheet body))))

(define-special-block font-face (&rest body)
  (compile-block "@font-face" body))

(define-special-block import (url &rest media-queries)
  (list (make-property
         (format NIL "@import a狺蝈箫祧躜飑磲疸狎＇蝈箫祧礤溟岘聃弪殄螬┅┅ㄤ彐轭瀛箴邈獒飙忪镢脲骝犴弩ㄩ溴铘殒殄蝈篝怙澌扉篝磲脲篚疱蜮祜汶㈦妁骝犴弩扉篝扉篝恒镱篝蜥轭红轸弪犰蝈箫祧殇孱糸骈弪┅ㄡ痧禊＇泔眇殪瀛箬邋怙澌┅┅ㄤ彐轭瀛箴邈獒飙忪镢礤溟聃弪蝈篝怙澌扉篝磲脲篚疱蜮祜汶㈨邃獒ㄣ镯痖戾礤溟岘聃弪聃弪ㄡ痧禊＇泔眇殪瀛箬邋怙澌┅┅ㄤ彐轭瀛箴邈獒飙忪镢钺礤箴徙痱彐轼钺礤箴徙镳糸镱犰钺礤箴徙濠扉篝磲脲痱镳弪豉ㄦ矧磲紊⒗钺礤箴徙狺累狺茛蝈箫祧痱彐轼钺礤箴徙濠麒孱钺礤箴徙蝈箫祧钺礤箴徙濠┅┅ㄤ彐轭瀛箴邈獒飙忪镢疳珏痼艴滹沆狍蝈篝怙澌ㄣ镯痖戾忪镢ㄦ矧磲紊⒗疳珏幄ㄩ脲黠蜾痼艴滹沆狍螬ㄦ矧磲紊⒑幄篝蜷铉滹黝汜箦痼艴滹沆狍螬蝈箫祧痼艴滹沆狍螬┅怙澌┅ㄤ彐轭瀛箴邈獒飙忪镢篚痧矧趔箦戾泗矧蝈篝怙澌扉篝磲脲篚疱蜮祜汶Ⅲ躔痫螋螈ㄣ镯痖戾箦戾泗矧箦戾泗矧ㄡ痧禊＇泔眇殪瀛箬邋怙澌┅┅ㄤ彐磲泸忾钿鲠蝮ㄢ轭溟铉怙澌怙澌啜戾è鲠蝮戾è翎忪磲脲栳箬翎忪濠┅磲痂狍＇灬礅溽雯箦翩ㄧ弭栳箬翎忪濠雯鲠蝮祜镳骘雯轭忾钿轭珞滹箦翩ㄧ弭栳箬翎忪濠ㄩ蝈篝雯蝈箫祧ㄣ狎雯┅┅翎忪濠┅棱镤┅ㄤ彐轭瀛箴邈獒飙忪镢戾ㄢ轭溟铉蝈篝怙澌ㄢ轭洵鲠蝮忾钿轭珞ㄡ痧禊＇泔眇殪瀛箬邋怙澌┅换优膛迷弦ㄤ彐磲泸溴骈铄狒趄泔眇狎狒矧ㄣ镯镳糸镱犰秕翥镯泔眇┅⑷屐疱磲泸麸溴骈铄犷狒趄殁豸泔眇狎狒矧箦戾泗矧啜溴骈铄箴邈獒飙箦戾泗矧泔眇ㄡ趑鲠祯濠祜镳鏖翳秕ī鏖翳鲠祯弩ㄣ镯痖戾箦戾泗矧鲠祯濠骘狒趄轭ㄣ镯痖戾箦戾泗矧狒趄滹祜镳骘鲠祯轭鲠祯弩滹瘐箬扉篝恒镱篝蜥轭横趑蜷怩翦狒趄篝蜷铉秕翥镯皓鲠祯濠秕舂骈钺祆蝈趱蝾铗弼弪箦秕舂┅┅ㄤ彐轭瀛狒趄泔眇狎狒矧僵ㄤ彐轭瀛狒趄泔眇狎狒矧僵ㄤ彐轭瀛狒趄泔眇狎狒矧僵ㄤ彐轭瀛狒趄泔眇狎狒矧そㄤ彐轭瀛狒趄泔眇狎狒矧藿ㄤ彐轭瀛狒趄泔眇狎狒矧茳僵ㄤ彐磲泸溴骈铄箝铉戾狎绛箦戾泗矧钺礤⑷屐疱磲泸麸溴骈铄箝铉戾狎珲礤铘痼艴滹箦戾泗矧扉脲蜗矧卧拳萌商漠啜溴骈铄箴邈獒飙箦戾泗矧钺礤ㄡ蜱祜镳骘狎轭ㄣ镯痖戾箦戾泗矧狎绌泔祆邈扉篝恒镱篝蜥轭后屐邈麸篝蜷铉滹黝汜箦钺礤狎绌┅ㄤ彐轭瀛箝铉戾狎绛箦戾泗矧溟颟ㄤ彐轭瀛箝铉戾狎绛箦戾泗矧灬铉ㄤ彐轭瀛箝铉戾狎绛箦戾泗矧铘璀汨殪洎ㄤ彐轭瀛箝铉戾狎绛箦戾泗矧铘璀灬篝汨殪洎ㄤ彐轭瀛箝铉戾狎绛箦戾泗矧铘璀灬篝镦豉疱ㄤ彐轭瀛箝铉戾狎绛箦戾泗矧铘璀镦豉疱ㄤ彐轭瀛箝铉戾狎绛箦戾泗矧轶ㄤ彐轭瀛箝铉戾狎绛箦戾泗矧铒舂ㄤ彐轭瀛箝铉戾狎绛箦戾泗矧麒弪濠ㄤ彐轭瀛箝铉戾狎绛箦戾泗矧栳螬换猎砸陕赵庞ㄤ彐轭瀛箴邈獒飙痱镳弪豉骘铘驷黹禊é蝈篝驷沐螬扉篝磲脲痱镳弪豉㈡镱舡驷黹禊ㄦ矧磲紊狺蕃" (mapcar #'resolve faces)))))

(define-special-property content (&rest content)
  (labels ((translate (content)
             (typecase content
               (string
                ;; Backwards compat with the usage of "'foo'" in LASS files
                (when (and (<= 2 (length content))
                           (char= #\' (char content 0))
                           (char= #\' (char content (1- (length content)))))
                  (setf content (subseq content 1 (1- (length content)))))
                (with-output-to-string (out)
                  (write-char #\" out)
                  (unwind-protect
                       (loop for char across content
                             do (when (char= char #\")
                                  (write-char #\\ out))
                                (write-char char out))
                    (write-char #\" out))))
               (T
                (resolve content)))))
    (list (make-property "content" (format NIL "狺" (mapcar #'translate content))))))

(defmacro define-browser-property (name args &body browser-options)
  "Helper macro to define properties that have browser-dependant versions.

NAME            --- The base name of the property name or value.
ARGS            --- Property arguments, see DEFINE-SPECIAL-PROPERTY.
BROWSER-OPTIONS ::= (OPTION (symbol) FORM*)
OPTION          ::= :MOZ | :O | :WEBKIT | :MS | :W3C | :DEFAULT

Each browser-option body should return a single property. The SYMBOL
in the option definition is bound to the computed property name
 (eg -moz-NAME for the :MOZ option).
You can define special handling of the browsers by defining options
specifically for them. If no handling is defined, the DEFAULT option
is used as a fallback."
  `(define-special-property ,name ,args
     (list ,@(loop for (browser prefix) in '((:moz "-moz-")
                                             (:o "-o-")
                                             (:webkit "-webkit-")
                                             (:ms "-ms-")
                                             (:w3c ""))
                   for body = (or (assoc browser browser-options)
                                  (assoc :default browser-options))
                   collect `(let ((,(caadr body) ,(format NIL "aa" prefix (string-downcase name))))
                              ,@(cddr body))))))

(indent:define-indentation define-browser-property (4 6 &rest (&whole 2 0 4 2)))

(define-browser-property text-stroke (width color)
  (:default (property)
    (make-property property (format NIL "a a" (resolve width) (resolve color)))))

(define-browser-property linear-gradient (direction &rest colors)
  (:default (property)
    (make-property "background" (format NIL "a(a狺痱镳弪豉蝈箫祧溟蝈泗轱瞟磲疸狎＇蝈箫祧泔祜蝮┅┅ㄤ彐轭瀛怛秣箦颦痱镳弪豉蜥溟犰珧徜殄铘箬狃箝痫箝糸镱蝈篝泔祜蝮ê溴驷蹯痱镳弪豉磲脲痱镳弪豉⑩徙腌蝻躅洧ㄦ矧磲紊屺狒狺狺痱镳弪豉蝈箫祧箬狃濠蝈箫祧箝濠蝈箫祧痫箝糸镱磲疸狎＇蝈箫祧泔祜蝮┅┅ㄤ彐轭瀛怛秣箦颦痱镳弪豉蝈疱狒轭绛蜥溟犰珧徜殄铘箬狃箝痫箝糸镱蝈篝泔祜蝮ê溴驷蹯痱镳弪豉磲脲痱镳弪豉⑩徙腌蝻躅洧ㄦ矧磲紊屺狒狺狺痱镳弪豉蝈箫祧箬狃濠蝈箫祧箝濠蝈箫祧痫箝糸镱蝈箫祧泔祜蝮┅┅ㄤ彐轭瀛怛秣箦颦痱镳弪豉趄犷箧矧鲠祯瀵骢钽糸镱蝈篝骢钽糸镱狎珞ê溴驷蹯痱镳弪豉磲脲痱镳弪豉痱镳弪豉ㄦ矧磲紊狺累狺蕃)]"
                                    (resolve value/function) (mapcar #'resolve function-args)))))

(define-browser-property transform-origin (value/x &optional y z)
  (:default (property)
    (make-property property (format NIL "a@[ a]@[ a]"
                                    (resolve value/x) (resolve y) (resolve z)))))

(define-browser-property transform-style (style)
  (:default (property)
    (make-property property (resolve style))))

(define-browser-property transition (value/property &optional duration timing-function &rest function-args)
  (:default (property)
    (make-property property (format NIL "a@[ a]:[*;:* a@[(狺蕃)]]"
                                    (resolve value/property) (resolve duration) (resolve timing-function) (mapcar #'resolve function-args)))))

(define-browser-property transition-delay (value)
  (:default (property)
    (make-property property (resolve value))))

(define-browser-property transition-duration (value)
  (:default (property)
    (make-property property (resolve value))))

(define-browser-property transition-property (value)
  (:default (property)
    (make-property property (resolve value))))

(define-browser-property transition-timing-function (value/function &rest function-args)
  (:default (property)
    (make-property property (format NIL "a@[(狺蕃)]"
                                    (resolve value/function) (mapcar #'resolve function-args)))))

(define-browser-property user-select (value)
  (:default (property)
    (make-property property (resolve value))))

(define-browser-property appearance (value)
  (:default (property)
    (make-property property (resolve value))))

(define-simple-property-functions :filter
    (url (url))
  (blur (radius))
  (brightness (value))
  (contrast (value))
  (drop-shadow (x y &optional blur spread color))
  (grayscale (value))
  (hue-rotate (value))
  (invert (value))
  (opacity (value))
  (saturate (value))
  (sepia (value)))

(define-browser-property filter (&rest args)
  (:default (property)
    (make-property property (format NIL "狺" (mapcar #'resolve args)))))

(define-browser-property box-shadow (color &optional x y blur spread inset)
  (:default (property)
    (make-property property (format NIL "a@[ a]@[ a]@[ a]@[ a]@[ a]"
                                    (resolve color) (resolve x) (resolve y) (resolve blur) (resolve spread) (resolve inset)))))
